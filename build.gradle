version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.37"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'org.jetbrains.dokka'

dokka {
    includes = [
            'src/main/kotlin/core/renderers/dok.md',
            'src/main/kotlin/core/renderers/viewRenderers/dok.md',
            'src/main/kotlin/di/dok.md',
            'src/main/kotlin/utils/elementCss/dok.md'
    ]
    outputFormat = 'markdown'
    impliedPlatforms = ["JS"] // This will force platform tags for all non-common sources e.g. "JVM"
    kotlinTasks {
        // dokka fails to retrieve sources from MPP-tasks so they must be set empty to avoid exception
        // use sourceRoot instead (see below)
        []
    }
    sourceRoot {
        // assuming there is only a single source dir...
        path = sourceSets.getByName("main").kotlin.srcDirs[0]
        platforms = ["JS"]
    }
}

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        kotlinOptions.outputFile = "${projectDir}/web/js/app.js"
        sourceMap = true
        moduleKind = "umd"
    }

    doLast {
        copy {
            from "build/node_modules_imported"
            into "web/js/lib"
        }
    }
}



kotlinFrontend {
    downloadNodeJsVersion = 'latest'

    npm {
        dependency "material-design-lite"
    }
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
}

repositories {
    flatDir {
        dirs 'libs'
    }
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:1.2.60"
    testCompile "org.jetbrains.kotlin:kotlin-test-js"

    compile(name: 'View-js', ext:'jar')

    compile 'org.kodein.di:kodein-di-erased-js:5.2.0'
    compile 'org.kodein.di:kodein-di-conf-js:5.2.0'
}

clean.doFirst {
    delete 'web/js/app', 'web/js/app.js', 'web/js/app.meta.js', 'web/js/lib'
}